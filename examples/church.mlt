# church encoding ruleset for mlatu
# inspired by https://www.youtube.com/watch?v=TcveznrwYtk

# church booleans:
# (a) (b) true<  |->  (a), (a) (b) false<  |->  (b)
true  = (-).
false = (~-).
not   = (~)~,.
and   = +<.
or    = +swapd<.
xor   = (~)~()~<~,.


# church pairs:
# (f) (a) (b) cons<  |->  (a) (b) f
# (a) (b) cons       |->  ( ((a) (b)) ~ (<) dip < )
cons = ~>~>,>(~(<)dip<),.
car  = ( -)~<.
cdr  = (~-)~<.


# church numbers:
# (a) 3<  |->  (a a a)
# etc.
succ = (+)~,(,),.
pred = (()~(+))~+(<--()),~(,)~<,,.
add  = (succ)~<<.
sub  = (pred)~<<.
mul  = ,.
exp  = <.
sqr  = +mul.
0 = (-()). 1 = 0 succ. 2 = 1 succ. 3 = 2 succ. 4 = 3 succ. 5 = 4 succ. 6 = 5 succ. 7 = 6 succ. 8 = 7 succ. 9 = 8 succ. 10 = 9 succ.
100 = 10 10 mul. 1000 = 100 10 mul. 10000 = 100 100 mul.

# GNU's definition
add2 = ((+)~)dip(~)~(,),,,,.
